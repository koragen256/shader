<html>
	<head>
		<style>
			/* キャンバスを元のサイズに拡大表示 */
			canvas {
				width: 100%;
				height: 100%;
			}
		</style>
		<!-- fragment shader -->
		<script id="fs" type="x-shader/x-fragment">
			precision highp float;
			
			uniform vec2 u_resolution;
			uniform float u_time;
			uniform vec2 u_mouse;
			
			const float e = exp(1.);
			const vec3 cPos = vec3(0,0,10);
			const vec3 light= vec3(1,1,1);
			
			struct Material{
				float dist;
				vec3 color;
				float fr;
			};
			
			// rotate
			vec3 rotate(vec3 p, float angle, vec3 axis){
				vec3 a = normalize(axis);
				float s = sin(angle);
				float c = cos(angle);
				float r = 1.0 - c;
				mat3 m = mat3(
					a.x * a.x * r + c,
					a.y * a.x * r + a.z * s,
					a.z * a.x * r - a.y * s,
					a.x * a.y * r - a.z * s,
					a.y * a.y * r + c,
					a.z * a.y * r + a.x * s,
					a.x * a.z * r + a.y * s,
					a.y * a.z * r - a.x * s,
					a.z * a.z * r + c
				);
				return m * p;
			}
			
			float smax(float a, float b, float r) {
				vec2 u = max(vec2(r + a,r + b), vec2(0));
				return min(-r, max (a, b)) + length(u);
			}
			
			float d_Sphere(vec3 p,float size){
				return length(p)-size;
			}
			
			float sdRoundBox( vec3 p, vec3 b, float r )
			{
			  vec3 q = abs(p) - b;
			  return length(max(q,0.0)) + min(max(q.x,max(q.y,q.z)),0.0) - r;
			}
			
			Material map(vec3 p){
				Material m;
				m.dist=1E5;
				float tm = d_Sphere(p,1000.);
				float t1 = d_Sphere(p,(sin(u_time*0.2)+1.)*3.+12.);
				t1 = smax(t1,sdRoundBox(p,vec3(12.0),0.),1.5);
				t1 = smax(tm,-t1,0.5);
				float t2 = d_Sphere(p+vec3(sin(u_time),cos(u_time),0.0),0.4);
			
				if(t1<m.dist){
					m.dist=t1;
					m.color=vec3(0,0,0);
					m.fr=1.;
				}
			
				if(t2<m.dist){
					m.dist=t2;
					m.color=vec3(1,1,1.2);
					m.fr=0.;
				}
			
				return m;
			}
			
			vec3 nMap( vec3 p ) {
				vec2 d = vec2( 0, 1E-3 );
			
				return normalize( vec3(
					map( p + d.yxx ).dist - map( p - d.yxx ).dist,
					map( p + d.xyx ).dist - map( p - d.xyx ).dist,
					map( p + d.xxy ).dist - map( p - d.xxy ).dist
				) );
			}
			
			vec3 trace(vec3 pos,vec3 ray, float refractiveIndex){
				ray = rotate(ray, -u_mouse.x/200., vec3(1,0.0,0));
				pos = rotate(pos, -u_mouse.x/200., vec3(1,0.0,0));
				ray = rotate(ray, u_time*0.1, vec3(0,1.0,0));
				pos = rotate(pos, u_time*0.1, vec3(0,1.0,0));
				pos*=1.;
				vec3 p = pos;
				vec3 color;
				float d;
				int w=0;
				for(int i=0;i<128;i++){
					Material m = map(p);
					d = m.dist;
					p+=d*ray;
					vec3 no = nMap(p);
					color += m.color*mix( 0.6, 1.0,dot(light,no))*0.02;
			
					if (d < 0.001 && m.fr>0.5&&w<1000) {
						ray = reflect(ray,no);
						p-= d*ray*16.;
						w++;
					}
					
				}
				return color;
			}
			
			void main() {
				vec2 pos = (2.0*gl_FragCoord.xy-u_resolution.xy)/min(u_resolution.x,u_resolution.y);
				vec3 ray = normalize((vec3(pos,0)-cPos)*vec3(2,2,1));
				gl_FragColor = vec4(trace(cPos,ray,0.6),1);
			}
		</script>
		<!-- /fragment shader -->

		<!-- vertex shader -->
		<script id="vs" type="x-shader/x-vertex">
			attribute vec3 position;
			void main(void){
			    gl_Position = vec4(position, 1.0);
			}
		</script>
		<!-- /vertex shader -->

		<script src="./script.js"></script>
	</head>
	<body style="margin: 0">
		<canvas id="canvas" style="display: inline" onclick="tgr()"> </canvas>
	</body>
</html>
